import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
  }
}

plugins {
    id "net.ltgt.apt" version "0.10"
}

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'jacoco'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'idea'
apply plugin: 'antlr'

repositories {
    mavenCentral()
    maven {
        url "http://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url "http://maven.atlassian.com/content/repositories/atlassian-public/"
    }
}



// -SNAPSHOT is added if the release task is not set
version = '3'
archivesBaseName = 'STT'

sourceCompatibility = 1.8
mainClassName='org.stt.StartWithJFX'

findbugs {
	excludeFilter = file("$rootProject.projectDir/config/findbugs/excludeFilter.xml")
}

configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

dependencies {
	antlr  group: "org.antlr", name: "antlr4", version: "4.7"
    compile group: "org.antlr", name: "antlr4-runtime", version: "4.7"
    compile group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.7-M5'
	compile 'org.yaml:snakeyaml:1.18'
    compile 'com.google.dagger:dagger:2.11'
    compile 'javax.inject:javax.inject:1'
    apt  'com.google.dagger:dagger-compiler:2.11'
    compile 'net.engio:mbassador:1.3.0'
    compile 'org.controlsfx:controlsfx:8.40.12'
	compile 'net.rcarz:jira-client:0.5'

    testCompile 'commons-io:commons-io:2.5'
	testCompile 'junit:junit-dep:4.11'
	testCompile 'org.hamcrest:hamcrest-core:1.3'
	testCompile 'org.hamcrest:hamcrest-library:1.3'
	testCompile 'org.mockito:mockito-all:2.0.2-beta'
}

jar {
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
	manifest.attributes("Main-Class":"org.stt.StartWithJFX")
	manifest.attributes("JavaFX-Feature-Proxy":"None")
}

processResources {
    filesMatching('version.info') {
        filter(ReplaceTokens, tokens: [
                "app.version": project.property("version"),
                "app.hash": getCheckedOutGitCommitHash()
        ])
    }
}

tasks.withType(FindBugs) {
	reports {
		xml.enabled = false
		html.enabled = true
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.5'
}

task release(dependsOn: 'distZip') {
    doLast {
	    println "Built release for $project.version"
    }
}

gradle.taskGraph.whenReady {taskGraph ->
	if (!taskGraph.hasTask(release)) {
		version += '-SNAPSHOT'
	}
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-long-messages"]
}

def getCheckedOutGitCommitHash() {
    def gitFolder = "$projectDir/.git/"
    def takeFromHash = 12
    /*
     * '.git/HEAD' contains either
     *      in case of detached head: the currently checked out commit hash
     *      otherwise: a reference to a file containing the current commit hash
     */
    def head = new File(gitFolder + "HEAD").text.split(":") // .git/HEAD
    def isCommit = head.length == 1 // e5a7c79edabbf7dd39888442df081b1c9d8e88fd
    // def isRef = head.length > 1     // ref: refs/heads/master

    if(isCommit) return head[0].trim().take(takeFromHash) // e5a7c79edabb

    def refHead = new File(gitFolder + head[1].trim()) // .git/refs/heads/master
    refHead.text.trim().take takeFromHash
}